plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao'
    id 'packer'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def keystorePSW = properties.getProperty("keystore.password")
def keystoreAlias = properties.getProperty("keystore.alias")
def keystoreAliasPSW = properties.getProperty("keystore.alias_password")
def keystoreFile = properties.getProperty("keystore.file")

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.yellowzero.listen"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 7
        versionName "1.0.6"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storePassword keystorePSW
            storeFile file(keystoreFile)
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        noCompress 'flac'
    }
}

packer {
    archiveNameFormat = '${appPkg}-${buildType}-v${versionName}-${channel}'
    archiveOutput = new File(project.rootProject.buildDir, "${android.defaultConfig.applicationId}_v${android.defaultConfig.versionName}_${android.defaultConfig.versionCode}")
    channelFile = project.rootProject.file("app/channels.txt")
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.lifecycle:lifecycle-livedata:2.2.0"
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    // 打包工具
    implementation 'com.mcxiaoke.packer-ng:helper:2.0.1'
    //网络请求工具
    implementation 'com.github.lygttpod:RxHttpUtils:2.3.3'
    //RecyclerView
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    //权限请求
    implementation 'com.hjq:xxpermissions:9.6'
    //文件缓存
    implementation 'com.danikula:videocache:2.7.1'
    //状态栏
    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    //popup菜单
    implementation 'com.github.razerdp:BasePopup:2.2.10'
    //数据库
    implementation 'org.greenrobot:greendao:3.3.0'
    //Gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    //Firebase
    implementation platform('com.google.firebase:firebase-bom:26.5.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    //sdp
    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.zzhoujay.richtext:richtext:3.0.8'

    //implementation 'q.rorbin:badgeview:1.1.3'
    // https://mvnrepository.com/artifact/com.hiczp/bilibili-api
    //implementation group: 'com.hiczp', name: 'bilibili-api', version: '0.2.0'
}